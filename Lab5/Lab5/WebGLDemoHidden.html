
<!DOCTYPE html>

<html>

<head>
<title>WebGL Final Lab</title>
<meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">

<script id="shader-vs" type="x-shader/x-vertex">
precision mediump float;
    attribute vec3 aVertexPosition;
    attribute vec3 aVertexNormal;
    attribute vec2 aVertexUV;

    uniform mat4 uMVPMatrix;
    uniform mat4 uMVMatrix;
    uniform mat4 uNMatrix;

    varying vec4 vColor; 
    varying vec3 vNormal_CameraSpace;
    varying vec3 vVertexPos_CameraSpace;
    varying vec2 vVertexUV;

    void main(void) {
        gl_Position = uMVPMatrix * vec4( aVertexPosition, 1.0 );
      //  vColor = aVertexColor; 
        vNormal_CameraSpace = vec3( uNMatrix * vec4( aVertexNormal, 0.0 ) );
        vVertexPos_CameraSpace = vec3( uMVMatrix * vec4( aVertexPosition, 1.0 ) );
        vVertexUV = aVertexUV;
    }
</script>

<script id="shader-fs" type="x-shader/x-fragment">
precision mediump float;
   // varying vec4 vColor; 
    varying vec3 vNormal_CameraSpace;
    varying vec3 vVertexPos_CameraSpace;
    varying vec2 vVertexUV;

    uniform vec3 uLightPos_CameraSpace;

    uniform vec4 uLightAmbient; 
    uniform vec4 uLightDiffuse; 
    uniform vec4 uLightSpecular;

    uniform float uShininess;

    uniform vec4 uMaterialAmbient; 
    uniform vec4 uMaterialDiffuse; 
    uniform vec4 uMaterialSpecular;

    uniform sampler2D uMeshTexture; 

    void main(void) 
        {
        vec4 texColor = texture2D( uMeshTexture, vVertexUV ); 

        vec3 lightVec = normalize( vec3( uLightPos_CameraSpace - vVertexPos_CameraSpace ) ); 

        vec3 eyeVec = normalize( -vec3( vVertexPos_CameraSpace ) );
        
        float ndotl = max( dot( vNormal_CameraSpace, lightVec ), 0.0 ); 

        vec4 diffuse = uLightDiffuse * ndotl;

        vec3 reflectVec = normalize( 2.0 * ndotl * vNormal_CameraSpace - lightVec );
        
        float rdotv = max( dot( reflectVec, eyeVec ), 0.0 );

        vec4 specular = vec4( 0, 0, 0, 1); 
        if ( ndotl > 0.0 ) 
            specular = uLightSpecular * pow( rdotv, uShininess ); 

        gl_FragColor = texColor * ( uLightAmbient * uMaterialAmbient + diffuse * uMaterialDiffuse ) + specular * uMaterialSpecular; 
        gl_FragColor.a = 1.0;
        //gl_FragColor = texColor;
       // gl_FragColor = uLightAmbient;
      //  gl_FragColor = vec4( vNormal, 1.0 );
        }
</script>

<script id="textureShader-vs" type="x-shader/x-vertex">
precision mediump float;
    attribute vec2 aVertexPosition;
    attribute vec2 aVertexUV;

    varying vec2 vVertexUV;

    void main(void) {
        vVertexUV = aVertexUV;
        gl_Position = vec4( aVertexPosition, 0, 1 );
    }
</script>

<script id="textureShader-fs" type="x-shader/x-fragment">
precision mediump float;
varying vec2 vVertexUV;

uniform sampler2D uTexture; 

void main(void) 
    {
    gl_FragColor = texture2D( uTexture, vVertexUV ); 
    gl_FragColor.a = 1.0;
    /*if( dot( gl_FragColor, vec4( 1, 1, 1, 0 ) ) >= 1.0 )
        {
        gl_FragColor = vec4( 1, 0, 0, 1 );
        }
    else
        {
        gl_FragColor = vec4( 0, 1, 0, 1 );
        }
    */
    //vec4 color = texture2D( uTexture, vVertexUV ); 
    //gl_FragColor = vec4( color.x, color.x, color.x, 1 );
    }
</script>

<script id="deferredGeometryShader-vs" type="x-shader/x-vertex">
    precision mediump float;
    attribute vec3 aVertexPosition;
    attribute vec3 aVertexNormal;
    attribute vec2 aVertexUV;

    uniform mat4 uMVPMatrix;
    uniform mat4 uMVMatrix;
    uniform mat4 uNMatrix;

    varying vec4 vPosition_CameraSpace;
    varying vec4 vNormal_CameraSpace;
    varying vec2 vVertexUV;

    void main(void) {
        gl_Position = uMVPMatrix * vec4( aVertexPosition, 1 );
        vPosition_CameraSpace = uMVMatrix * vec4( aVertexPosition, 1 );
        vNormal_CameraSpace = uNMatrix * vec4( aVertexNormal, 0 );
        vVertexUV = aVertexUV;
    }
</script>

 <script id="deferredGeometryShader-fs" type="x-shader/x-fragment">
#extension GL_EXT_draw_buffers : require
    precision mediump float;

    varying vec4 vPosition_CameraSpace;
    varying vec4 vNormal_CameraSpace;
    varying vec2 vVertexUV;

    uniform sampler2D uMeshTexture; 
    uniform float uShininess;
    uniform vec4 uMaterialAmbient;
    uniform vec4 uMaterialDiffuse;
    uniform vec4 uMaterialSpecular;
    
void main(void) 
    {
    vec4 textureColor = texture2D( uMeshTexture, vVertexUV );
    
       gl_FragData[ 0 ] = vPosition_CameraSpace; 
     gl_FragData[ 0 ].a = uShininess;
    
    // Warning: do NOT normalize vec4!
    gl_FragData[ 1 ].xyz = normalize( vNormal_CameraSpace.xyz ); 
    gl_FragData[ 1 ].a = uMaterialSpecular.x;

    gl_FragData[ 2 ] = textureColor * uMaterialAmbient; 
    gl_FragData[ 2 ].a = uMaterialSpecular.y;

    gl_FragData[ 3 ] = textureColor * uMaterialDiffuse; 
    gl_FragData[ 3 ].a = uMaterialSpecular.z;
    }
</script>


<script id="deferredLightShader-vs" type="x-shader/x-vertex">
    precision mediump float;
    attribute vec2 aVertexPosition;
    attribute vec2 aVertexUV;

    varying vec2 vVertexUV;

    void main(void) {
        gl_Position = vec4( aVertexPosition, 0.5, 1 );
        vVertexUV = aVertexUV;
    }
</script>

 <script id="deferredLightShader-fs" type="x-shader/x-fragment">
#extension GL_EXT_draw_buffers : require
    precision mediump float;
    const int MAXIMUM_LIGHT_SUPPORTED = 250;
    varying vec2 vVertexUV;

    uniform sampler2D uPositionTex_CameraSpace; 
    uniform sampler2D uNormalTex_CameraSpace; 
    uniform sampler2D uAlbedoTex; 
    uniform sampler2D uMaterialDiffuseTex; 
    uniform sampler2D uBlurTex; 

    uniform int uLightNum;
    uniform float uLightRadiusSqr;

    uniform vec3 uLightPos_CameraSpace[ MAXIMUM_LIGHT_SUPPORTED ];
    uniform vec4 uLightAmbient[ MAXIMUM_LIGHT_SUPPORTED ]; 
    uniform vec4 uLightDiffuse[ MAXIMUM_LIGHT_SUPPORTED ]; 
    uniform vec4 uLightSpecular[ MAXIMUM_LIGHT_SUPPORTED ];

    uniform int uUseSSAO;

    uniform float uBrightnessThreshold;

void main(void) 
    {
     // This texture stores position + shininess 
    vec4 temp = texture2D( uPositionTex_CameraSpace, vVertexUV );
    vec3 vertexPos_CameraSpace = temp.xyz;
    float shininess = temp.w;

    temp = texture2D( uNormalTex_CameraSpace, vVertexUV );
    vec3 vertexNormal_CameraSpace = temp.xyz;
    vec4 materialSpecular = vec4( temp.w, 0, 0, 1 );

    temp = texture2D( uAlbedoTex, vVertexUV );  
    vec4 albedo = vec4( temp.xyz, 1 ) * ( uUseSSAO == 1 ? texture2D( uBlurTex, vVertexUV ).x: 1.0 );
    materialSpecular.y = temp.w;

    temp = texture2D( uMaterialDiffuseTex, vVertexUV );
    vec4 materialDiffuse = vec4( temp.xyz, 1 ); 
    materialSpecular.z = temp.w;
    
    gl_FragData[ 0 ] = vec4( 0.0 );
    for( int i = 0; i < MAXIMUM_LIGHT_SUPPORTED; ++i )
        {
        if( i == uLightNum )
            {
            break;
            }
        vec3 distanceVec = uLightPos_CameraSpace[ i ] - vertexPos_CameraSpace;
        if( dot( distanceVec, distanceVec ) > uLightRadiusSqr )
            {
           // gl_FragColor += albedo * uLightAmbient[ i ];
            continue;
            }
        vec3 lightVec = normalize( distanceVec ); 

        vec3 eyeVec = normalize( -vertexPos_CameraSpace );
      
        float ndotl = max( dot( vertexNormal_CameraSpace, lightVec ), 0.0 ); 
        if( ndotl <= 0.0 )
            {
            gl_FragData[ 0 ] += albedo * uLightAmbient[ i ];
            continue;
            }
        vec4 diffuse = uLightDiffuse[ i ] * ndotl;

        vec3 reflectVec = normalize( 2.0 * ndotl * vertexNormal_CameraSpace - lightVec );
        
        float rdotv = max( dot( reflectVec, eyeVec ), 0.0 );

        vec4 specular = uLightSpecular[ i ] * pow( rdotv, shininess ); 

        gl_FragData[ 0 ] += albedo * uLightAmbient[ i ] + diffuse * materialDiffuse + specular * materialSpecular; 
        }
    gl_FragData[ 0 ].a = 1.0;
    
    if( dot( gl_FragData[ 0 ].xyz, vec3(0.2126, 0.7152, 0.0722) ) >= uBrightnessThreshold ) // convert to gray scale
        {
        gl_FragData[ 1 ] = gl_FragData[ 0 ];
        }
     else
        {
        //gl_FragData[ 1 ] = vec4( 0, 0, 0, 1 );
        }
   //  gl_FragData[ 1 ] = vec4( 0, 1, 0, 1 );
    }
</script>

    
<script id="SSAOShader-vs" type="x-shader/x-vertex">
    precision mediump float;
    attribute vec2 aVertexPosition;
    attribute vec2 aVertexUV;

    varying vec2 vVertexUV;

    void main(void) {
        gl_Position = vec4( aVertexPosition, 0, 1 );
        vVertexUV = aVertexUV;
    }
</script>

 <script id="SSAOShader-fs" type="x-shader/x-fragment">
    precision mediump float;
    const int MAXIUM_SAMPLE_POINTS_SUPPORTED = 100;
    varying vec2 vVertexUV;

    uniform sampler2D uPositionTex_CameraSpace; 
    uniform sampler2D uNormalTex_CameraSpace; 
    uniform sampler2D uNoiseTex;

    uniform vec2 uNoiseScale;
    uniform vec3 uSamplePoints[ MAXIUM_SAMPLE_POINTS_SUPPORTED ];
    uniform int uSampleNum;
    uniform mat4 uPMatrix;
    uniform float uSampleRadius;
    uniform float uSSAOPower;

    void main(void) {
        vec3 normalVec = texture2D( uNormalTex_CameraSpace ,vVertexUV ).xyz;
        vec3 originPoint = texture2D( uPositionTex_CameraSpace ,vVertexUV ).xyz;
        
        // Generate TBN matrix
        // find random vector
        vec3 randomVec = texture2D( uNoiseTex, vVertexUV * uNoiseScale ).xyz;
        vec3 tangentVec = normalize( randomVec - normalVec * dot( normalVec, randomVec ) );
        vec3 biTangentVec = normalize( cross( normalVec, tangentVec ) );
        mat3 tbn = mat3( tangentVec, biTangentVec, normalVec );
        
        float occlusion = 0.0;
        vec3 test;
        for( int i = 0; i < MAXIUM_SAMPLE_POINTS_SUPPORTED; ++i )
            {
            if( i == uSampleNum )
                {
                break;
                }

            // This is still a vector
            // get sample vector in camera space
            vec3 samplePoint = tbn * uSamplePoints[ i ];

            // Convert to point
            samplePoint = originPoint + samplePoint * uSampleRadius;
            
            // Find the project point in clip space
            vec4 p = uPMatrix * vec4( samplePoint, 1 );
            vec3 projectPoint = p.xyz / p.w;
            projectPoint = ( projectPoint + vec3( 1, 1, 1 ) ) * 0.5;
            projectPoint = texture2D( uPositionTex_CameraSpace, projectPoint.xy ).xyz;
 
            // sample point is behind the projected point -> occolued
            occlusion += ( -projectPoint.z <= -samplePoint.z && abs( projectPoint.z - samplePoint.z ) <= uSampleRadius ) ? 1.0: 0.0;
            }
     
        // inverse the occlusion value, higher occlusion means darker
        occlusion = pow( 1.0 - occlusion / ( float( uSampleNum ) ), uSSAOPower );
        gl_FragColor = vec4( occlusion, occlusion, occlusion, 1 );

      //  gl_FragColor = vec4( -originPoint.z, -originPoint.z, -originPoint.z, 1 );
    }
</script>

<script id="blurShader-vs" type="x-shader/x-vertex">
    precision mediump float;
    attribute vec2 aVertexPosition;
    attribute vec2 aVertexUV;

    varying vec2 vVertexUV;

    void main(void) {
        gl_Position = vec4( aVertexPosition, 0, 1 );
        vVertexUV = aVertexUV;
    }
</script>


 <script id="blurShader-fs" type="x-shader/x-fragment">
    precision mediump float;
    const int NOISE_TEXTURE_SIZE = 4;
    const int HALF_NOISE_TEXTURE_SIZE = NOISE_TEXTURE_SIZE / 2;
    const int SAMPLE_COUNT = ( HALF_NOISE_TEXTURE_SIZE * 2 + 1 ) * ( HALF_NOISE_TEXTURE_SIZE * 2 + 1 );
    varying vec2 vVertexUV;

    uniform sampler2D uSSAOTex;
    uniform vec2 uInvTexureSize;

    void main(void) {

        gl_FragColor = vec4( 0 );
        for( int x = -HALF_NOISE_TEXTURE_SIZE; x <= HALF_NOISE_TEXTURE_SIZE; ++x )
            {
            for( int y = -HALF_NOISE_TEXTURE_SIZE; y <= HALF_NOISE_TEXTURE_SIZE; ++y )
                {
                vec2 shift = vec2( x, y ) * uInvTexureSize;
                gl_FragColor += texture2D( uSSAOTex, shift + vVertexUV );
                }
            }
        gl_FragColor /= float( SAMPLE_COUNT );
    }
</script>


<script id="textureMeshShader-vs" type="x-shader/x-vertex">
precision mediump float;
    attribute vec3 aVertexPosition;
    attribute vec2 aVertexUV;

    uniform mat4 uMVPMatrix;

    varying vec2 vVertexUV;

    void main(void) {
        gl_Position = uMVPMatrix * vec4( aVertexPosition, 1.0 );
        vVertexUV = aVertexUV;
    }
</script>

<script id="textureMeshShader-fs" type="x-shader/x-fragment">
#extension GL_EXT_draw_buffers : require
precision mediump float;
    varying vec2 vVertexUV;

    uniform sampler2D uMeshTexture; 
    uniform float uBrightnessThreshold;
    void main(void) 
        {
        gl_FragData[ 0 ] = texture2D( uMeshTexture, vVertexUV ); 
      if( dot( gl_FragData[ 0 ].xyz, vec3(0.2126, 0.7152, 0.0722) ) > uBrightnessThreshold ) // convert to gray scale
        {
        gl_FragData[ 1 ] = gl_FragData[ 0 ];
        }
     else
        {
   //     gl_FragData[ 1 ] = vec4( 0, 0, 0, 1 );
        }
        }
</script>

<script id="lightShapeShader-vs" type="x-shader/x-vertex">
precision mediump float;
    attribute vec3 aVertexPosition;

    uniform mat4 uMVPMatrix;

    void main(void) {
        gl_Position = uMVPMatrix * vec4( aVertexPosition, 1.0 );
    }
</script>

<script id="lightShapeShader-fs" type="x-shader/x-fragment">
#extension GL_EXT_draw_buffers : require
precision mediump float;
    uniform vec4 uLightColor; 
    uniform float uBrightnessThreshold;
    void main(void) 
        {
        gl_FragData[ 0 ] = uLightColor; 
    if( dot( gl_FragData[ 0 ].xyz, vec3(0.2126, 0.7152, 0.0722) ) >= uBrightnessThreshold ) // convert to gray scale
        {
        gl_FragData[ 1 ] = gl_FragData[ 0 ];
        }
     else
        {
       // gl_FragData[ 1 ] = vec4( 0, 0, 0, 1 );
        }
        }
</script>

<script id="gaussianBlurShader-vs" type="x-shader/x-vertex">
    precision mediump float;
    attribute vec2 aVertexPosition;
    attribute vec2 aVertexUV;

    varying vec2 vVertexUV;

    void main(void) {
        gl_Position = vec4( aVertexPosition, 0, 1 );
        vVertexUV = aVertexUV;
    }
</script>

<script id="gaussianBlurShader-fs" type="x-shader/x-fragment">
precision mediump float;
    
    varying vec2 vVertexUV;

    // const array is not supported
    const float GAUSSIAN_WEIGHTS_0 = 0.2270270270;
    const float GAUSSIAN_WEIGHTS_1 = 0.1945945946;
    const float GAUSSIAN_WEIGHTS_2 = 0.1216216216;
    const float GAUSSIAN_WEIGHTS_3 = 0.0540540541;
    const float GAUSSIAN_WEIGHTS_4 = 0.0162162162;

    uniform sampler2D uReferenceTexture; 
    uniform int uDirection;
    uniform vec2 uInvTexureSize;

    void main(void) 
        {
        gl_FragColor = texture2D( uReferenceTexture, vVertexUV ) * GAUSSIAN_WEIGHTS_0; 
        if( ( uDirection == 0 ) )
            {    
            gl_FragColor += texture2D( uReferenceTexture, vVertexUV + vec2( 1, 0 ) * uInvTexureSize ) * GAUSSIAN_WEIGHTS_1; 
            gl_FragColor += texture2D( uReferenceTexture, vVertexUV - vec2( 1, 0 ) * uInvTexureSize ) * GAUSSIAN_WEIGHTS_1; 
                
            gl_FragColor += texture2D( uReferenceTexture, vVertexUV + vec2( 2, 0 ) * uInvTexureSize ) * GAUSSIAN_WEIGHTS_2; 
            gl_FragColor += texture2D( uReferenceTexture, vVertexUV - vec2( 2, 0 ) * uInvTexureSize ) * GAUSSIAN_WEIGHTS_2; 
                
            gl_FragColor += texture2D( uReferenceTexture, vVertexUV + vec2( 3, 0 ) * uInvTexureSize ) * GAUSSIAN_WEIGHTS_3; 
            gl_FragColor += texture2D( uReferenceTexture, vVertexUV - vec2( 3, 0 ) * uInvTexureSize ) * GAUSSIAN_WEIGHTS_3; 
                
            gl_FragColor += texture2D( uReferenceTexture, vVertexUV + vec2( 4, 0 ) * uInvTexureSize ) * GAUSSIAN_WEIGHTS_4; 
            gl_FragColor += texture2D( uReferenceTexture, vVertexUV - vec2( 4, 0 ) * uInvTexureSize ) * GAUSSIAN_WEIGHTS_4; 
                
            }
        else
            {
            gl_FragColor += texture2D( uReferenceTexture, vVertexUV + vec2( 0, 1 ) * uInvTexureSize ) * GAUSSIAN_WEIGHTS_1; 
            gl_FragColor += texture2D( uReferenceTexture, vVertexUV - vec2( 0, 1 ) * uInvTexureSize ) * GAUSSIAN_WEIGHTS_1; 
                
            gl_FragColor += texture2D( uReferenceTexture, vVertexUV + vec2( 0, 2 ) * uInvTexureSize ) * GAUSSIAN_WEIGHTS_2; 
            gl_FragColor += texture2D( uReferenceTexture, vVertexUV - vec2( 0, 2 ) * uInvTexureSize ) * GAUSSIAN_WEIGHTS_2; 
                
            gl_FragColor += texture2D( uReferenceTexture, vVertexUV + vec2( 0, 3 ) * uInvTexureSize ) * GAUSSIAN_WEIGHTS_3; 
            gl_FragColor += texture2D( uReferenceTexture, vVertexUV - vec2( 0, 3 ) * uInvTexureSize ) * GAUSSIAN_WEIGHTS_3; 
                
            gl_FragColor += texture2D( uReferenceTexture, vVertexUV + vec2( 0, 4 ) * uInvTexureSize ) * GAUSSIAN_WEIGHTS_4; 
            gl_FragColor += texture2D( uReferenceTexture, vVertexUV - vec2( 0, 4 ) * uInvTexureSize ) * GAUSSIAN_WEIGHTS_4; 
             
            }
        gl_FragColor.a = 1.0;
        }
</script>

<script id="lightCombineShader-vs" type="x-shader/x-vertex">
    precision mediump float;
    attribute vec2 aVertexPosition;
    attribute vec2 aVertexUV;

    varying vec2 vVertexUV;

    void main(void) {
        gl_Position = vec4( aVertexPosition, 0, 1 );
        vVertexUV = aVertexUV;
    }
</script>

<script id="lightCombineShader-fs" type="x-shader/x-fragment">
precision mediump float;
    
    varying vec2 vVertexUV;

    uniform sampler2D uLightTexture; 
    uniform sampler2D uSceneTexture; 

    void main(void) 
        {
        vec4 lightColor = texture2D( uLightTexture, vVertexUV ); 
        vec4 sceneColor = texture2D( uSceneTexture, vVertexUV ); 
        gl_FragColor = lightColor + sceneColor;
        gl_FragColor.a = 1.0;
        }
</script>

<script type="text/javascript" src="scripts\gl-matrix\common.js"></script>
<script type="text/javascript" src="scripts\gl-matrix\mat2.js"></script>
<script type="text/javascript" src="scripts\gl-matrix\mat2d.js"></script>
<script type="text/javascript" src="scripts\gl-matrix\mat3.js"></script>
<script type="text/javascript" src="scripts\gl-matrix\mat4.js"></script>
<script type="text/javascript" src="scripts\gl-matrix\vec3.js"></script>
<script type="text/javascript" src="scripts\gl-matrix\vec4.js"></script>
<script type="text/javascript" src="scripts\gl-matrix\quat.js"></script>
<script type="text/javascript" src="scripts\gl-matrix\vec2.js"></script>


<script type="text/javascript" src="scripts\webgl-utils.js"></script>

<script type="text/javascript" src="scripts\Resources\Resource.js"></script>
<script type="text/javascript" src="scripts\Resources\ShaderResource.js"></script>

<script type="text/javascript" src="scripts\GlobalConstants.js"></script>
<script type="text/javascript" src="scripts\Controller.js"></script>
<script type="text/javascript" src="scripts\CameraController.js"></script>
<script type="text/javascript" src="scripts\DemoController.js"></script>
<script type="text/javascript" src="scripts\Transform.js"></script>
<!--<script type="text/javascript" src="scripts\TransformStack.js"></script>-->
<script type="text/javascript" src="scripts\Scene.js"></script>
<script type="text/javascript" src="scripts\LightManager.js"></script>

<script type="text/javascript" src="scripts\SceneNodes\SceneNodes.js"></script>
<script type="text/javascript" src="scripts\SceneNodes\CameraNode.js"></script>
<script type="text/javascript" src="scripts\SceneNodes\GeometrySceneNodes.js"></script>
<script type="text/javascript" src="scripts\SceneNodes\MeshSceneNodes.js"></script>
<script type="text/javascript" src="scripts\SceneNodes\TextureMeshSceneNode.js"></script>
<script type="text/javascript" src="scripts\SceneNodes\PointLightSceneNode.js"></script>
<script type="text/javascript" src="scripts\SceneNodes\LightControlNode.js"></script>
<script type="text/javascript" src="scripts\SceneNodes\TeapotControlNode.js"></script>

<script type="text/javascript" src="scripts\shaders_setup.js"></script>
<script type="text/javascript" src="scripts\Main.js"></script>
<script type="text/javascript" src="scripts\Resources\GeometryResources.js"></script>
<script type="text/javascript" src="scripts\Drawers\TextureDrawer.js"></script>
<script type="text/javascript" src="scripts\Drawers\DeferredDrawer.js"></script>
<script type="text/javascript" src="scripts\Drawers\SSAODrawer.js"></script>
<script type="text/javascript" src="scripts\Drawers\BloomDrawer.js"></script>
     <audio id="AudioFile">
         <source src="Music.mp3" type="audio/mpeg">
        </audio> 
</head>


<body onload="webGLStart();">

    <canvas id="WebGL-canvas" style="border: none;" width="1024" height="512"></canvas>

    <br/>

    <h4 id="CameraControl">- Control Camera</h4> 
            <h4>*   W, S, A, D, F, C: Forwad, Backward, Left, Right, Up, Down</br> 
                *   Arrow Keys: Rotation</br> </h4> 

    <h4 id="MusicControlOption" value="Music.mp3">- Control Music</br> </br> </h4> 

    <h4 id="RenderControlOption">- Control Render Output</br> </br> </h4> 

    <h4 id="SSAOControlOption">- Control SSAO</br> </br> </h4>   

    <h4 id="LightControlOption">- Control Light</br> </br> </h4> 

    <h4 id="BloomControlOption">- Control Bloom</br> </br> </h4> 


</body>


</html>

